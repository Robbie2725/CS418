<!DOCTYPE HTML>
<html lang="en">
<head>
<title>Hello Mesh </title>
<meta charset="utf-8">
</head>


<script id="shader-vs-skybox" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;

    uniform mat4 uMMatrix;
    uniform mat4 uVMatrix;
    uniform mat4 uPMatrix;

    varying vec3 vNormal;

    void main(void) {

    gl_Position = uPMatrix*uVMatrix*uMMatrix*vec4(aVertexPosition, 1.0);

    vNormal = normalize(aVertexPosition);
    }
</script>

<script id="shader-fs-skybox" type="x-shader/x-fragment">
precision mediump float;
varying vec3 vNormal;

uniform samplerCube uTexture;
void main(void) {
        gl_FragColor = textureCube(uTexture, normalize(vNormal));
 }
</script>

<script id="shader-vs-teapot" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;

    uniform int select;

    uniform mat4 uMMatrix;
    uniform mat4 uVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    varying vec3 fragNormal;
    varying vec3 vPositionEye;

    varying vec3 vWorldNormal;
    varying vec3 vWorldPosition;
    void main(void) {

    // calculate the vertex position
    gl_Position = uPMatrix*uVMatrix*uMMatrix*vec4(aVertexPosition, 1.0);
    if(select==0 || select==1){
      // get the position in world coordinates for frag shader
      vWorldPosition = mat3(uMMatrix)*aVertexPosition;
      // get the normal in world coordinates for frag shader
      vWorldNormal = mat3(uMMatrix)*aVertexNormal;
    }
    else{
      // Get the vertex position in eye coordinates
      vec4 vertexPositionEye4 = uVMatrix*uMMatrix * vec4(aVertexPosition, 1.0);
      vPositionEye = vertexPositionEye4.xyz / vertexPositionEye4.w;

      //Transform the normal to eye coordinates, pass to frag shader
      fragNormal = uNMatrix*aVertexNormal;

    }
    }
</script>

<script id="shader-fs-teapot" type="x-shader/x-fragment">
precision mediump float;

uniform vec3 uLightPosition;
uniform vec3 uDiffuseLightColor;
uniform vec3 uAmbientLightColor;
uniform vec3 uSpecularLightColor;

uniform vec3 uKAmbient;
uniform float uShininess;
uniform vec3 uKSpecular;
uniform vec3 uKDiffuse;

varying vec3 wNormal;
varying vec3 wPosition;
varying vec3 fragNormal;
varying vec3 vPositionEye;

varying vec3 vWorldNormal;
varying vec3 vWorldPosition;

uniform int fSelect;

uniform vec3 worldCameraPosition;
uniform samplerCube uTexture;
void main(void) {
      if(fSelect==0 || fSelect==1){
        // calculate and normalize the vectors for reflection/refraction
        vec3 worldNormal = normalize(vWorldNormal);
        vec3 eyeToSurface = normalize(vWorldPosition-worldCameraPosition);
        if(fSelect == 0){
          //reflect and get texture from map
          vec3 reflection = reflect(eyeToSurface, worldNormal);
          gl_FragColor = textureCube(uTexture, reflection);
        }
        else {
          //refract and get texture from map
          vec3 refraction = refract(eyeToSurface, worldNormal, .9);
            vec4 refractColor = textureCube(uTexture, refraction);
            // mix with white
            refractColor = mix(refractColor, vec4(1,1,1,1), 0.2);
            gl_FragColor = refractColor;
        }
      }
      else{
        // normalize normal vector from the vertex shader
        vec3 normalVector = normalize(fragNormal);

        // get the vector to the light source
        vec3 lightVector = normalize(uLightPosition-vPositionEye);

        // Calculate the ambient light component (ambient light color * reflectivity)
        vec3 ka_ia = uKAmbient*uAmbientLightColor;

        // Weight of the diffuse light component (Light vector dot Normal vector)
        float LdotN = max(dot(lightVector, normalVector), 0.0); // Diffuse lighting weight

        //Calculate the diffuse term, i.e. k_d * i_d
        // Use the k_d passed in from the vertex shader
        vec3 diffuseTerm = uKDiffuse*uDiffuseLightColor;


        //Now need to calculate the specular term and its weight
        float specWeight=0.0;
        float HdotN=0.0;
        vec3 specTerm = uKSpecular*uSpecularLightColor;
        if(LdotN > 0.0){
            vec3 viewVector = normalize(-vPositionEye); // get the view vector (0-eye position)
            vec3 halfwayVector = normalize(viewVector+lightVector); //get the normalized halfway vector
            HdotN = max(dot(halfwayVector, normalVector), 0.0); // Calculate specular weight (H dot N)
            specWeight = pow(HdotN, uShininess); //raise to the shininess exponent
        }

        // Set the color to the sum of each component
        gl_FragColor = vec4(ka_ia+(LdotN*diffuseTerm)+(specWeight*specTerm), 1);
      }
 }
</script>

<script src="gl-matrix-min.js"></script>
<script src="webgl-utils.js"></script>
<script src="TriMesh.js"></script>
<script src="mp3.js"></script>
<script src="CubeMap.js"></script>
<body onload="startup();">
 <canvas id="myGLCanvas" width="800" height="800"></canvas>
    <form id="input_form">
        <fieldset>
            <legend>Rendering Parameters</legend>
         <input type="radio" name="primitive" id="refl" value="wireframe" checked> Reflection
         <input type="radio" name="primitive" id="refr" id="polygon" value="polygon"> Refraction
         <input type="radio" name="primitive" id="phong" value="wirepoly" > Normal (blinn-phong)
        </fieldset>
        <fieldset>
            <legend>Mesh Pose and Camera Position</legend>
         <emph>Euler Y:</emph> <input type="number" id="eY" value="0"> Keys A and D <br><br>
         <emph>Euler X:</emph> <input type="number" id="eX" value="0"> Keys Left and Right Arrow <br><br>
        </fieldset>
        </form>
</body>

</html>
